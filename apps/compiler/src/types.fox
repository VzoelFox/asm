# Type System for Self-Host Compiler
# Provides compile-time type checking to catch bugs before runtime
# NOTE: No Ambil here - inherits from main.fox parent
# ID Range: 1040-1047

### 1040
# Type Constants
const TYPE_UNKNOWN = 0
const TYPE_INT = 1
const TYPE_FLOAT = 2
const TYPE_STRING = 3
const TYPE_STRUCT = 4
const TYPE_POINTER = 5
const TYPE_VOID = 6

# Global Type Registry
var var_type_map = 0
var struct_field_types = 0
var type_errors = 0
var type_error_count = 0

### 1041
fungsi init_type_system()
   var vtm = map_create(256)
   var_type_map = vtm
   var sft = map_create(64)
   struct_field_types = sft
   var errs = vec_create(32)
   type_errors = errs
   type_error_count = 0
tutup_fungsi

### 1042
fungsi register_variable_type(var_name, type_id)
   var vtm = var_type_map
   panggil map_put(vtm, var_name, type_id)
tutup_fungsi

### 1043
fungsi register_struct_variable(var_name, struct_name)
   var vtm = var_type_map
   panggil map_put(vtm, var_name, TYPE_STRUCT)
   var key = str_concat("struct_", var_name)
   panggil map_put(vtm, key, struct_name)
tutup_fungsi

### 1044
fungsi get_variable_type(var_name)
   var vtm = var_type_map
   var type_id = map_get(vtm, var_name)
   jika (type_id == 0)
      asm_mulai
      mov rax, 0
      tutup_asm
      kembalikan
   tutup_jika
   asm_mulai
   mov rax, [var_type_id]
   tutup_asm
   kembalikan
tutup_fungsi

### 1045
fungsi get_struct_name(var_name)
   var vtm = var_type_map
   var key = str_concat("struct_", var_name)
   var struct_name = map_get(vtm, key)
   asm_mulai
   mov rax, [var_struct_name]
   tutup_asm
   kembalikan
tutup_fungsi

### 1046
fungsi report_type_errors()
   jika (type_error_count > 0)
      cetak("=== TYPE ERRORS DETECTED ===")
      cetak(type_error_count)
      var errs = type_errors
      var count = vec_len(errs)
      var idx = 0
      selama (idx < count)
         var err_msg = vec_get(errs, idx)
         cetak_str(err_msg)
         idx = idx + 1
      tutup_selama
      cetak("=== COMPILATION FAILED ===")
      asm_mulai
      mov rax, 60
      mov rdi, 1
      syscall
      tutup_asm
   tutup_jika
tutup_fungsi

### 1047
fungsi infer_literal_type(literal_str)
   var first_ch = str_get(literal_str, 0)
   jika (first_ch == 34)
      asm_mulai
      mov rax, 3
      tutup_asm
      kembalikan
   tutup_jika
   var len = str_len(literal_str)
   var idx = 0
   selama (idx < len)
      var ch = str_get(literal_str, idx)
      jika (ch == 46)
         asm_mulai
         mov rax, 2
         tutup_asm
         kembalikan
      tutup_jika
      idx = idx + 1
   tutup_selama
   asm_mulai
   mov rax, 1
   tutup_asm
   kembalikan
tutup_fungsi
